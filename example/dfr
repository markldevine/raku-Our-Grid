#!/usr/bin/env raku

use lib '/home/mdevine/github.com/raku-Our-Grid/lib';

use Async::Command::Multi;
use Our::Grid;
use Our::Grid::Cell;
use Our::Utilities;
use Our::Cache;
use JSON::Fast;

my %server-results;

class df-Pk {
    has Str $.filesystem;
    has     $.total-bytes;
    has     $.used-bytes;
    has     $.available-bytes;
    has Str $.capacity;
    has Int $.cap-percent-num;
    has Str $.mount-point;
}

grammar df-Pk-grammar {
    token TOP {
        ^
        Filesystem \s+ 1024 '-' blocks \s+ Used \s+ Available \s+ Capacity \s+ Mounted \s+ on $$
        <df-data-record>+
        $
    }
    token df-data-record {
        \n ^^ <filesystem> \s+ <total-KBs> \s+ <used-KBs> \s+ <available-KBs> \s+ <capacity> \s+ <mount-point> $$
    }
    token filesystem        { <[-\.:/_\w]>+                 }
    token total-KBs         { '-' || <total-KBs-num>        }
    token total-KBs-num     { \d+                           }
    token used-KBs          { '-' || <used-KBs-num>         }
    token used-KBs-num      { \d+                           }
    token available-KBs     { '-' || <available-KBs-num>    }
    token available-KBs-num { \d+                           }
    token capacity          { '-' || <capacity-percent>     }
    token capacity-percent  { <cap-percent-num> '%'         }
    token cap-percent-num   { \d+                           }
    token mount-point       { <[-\.:/_\w]>+                 }
}

class df-Pk-actions {
    has $.server        is required;

    method df-data-record ($/) {
        my $total-bytes         = '-';
        if $/<total-KBs><total-KBs-num>:exists {
            $total-bytes        = +$/<total-KBs><total-KBs-num> * 1024;
        }
        my $used-bytes          = '-';
        if $/<used-KBs><used-KBs-num>:exists {
            $used-bytes         = +$/<used-KBs><used-KBs-num> * 1024;
        }
        my $available-bytes     = '-';
        if $/<available-KBs><available-KBs-num>:exists {
            $available-bytes    = +$/<available-KBs><available-KBs-num> * 1024;
        }
        my $cap-percent-num     = 0;
        if $/<capacity><capacity-percent><cap-percent-num>:exists {
            $cap-percent-num    = +$/<capacity><capacity-percent><cap-percent-num>;
        }
        %server-results{$!server}.push: 
            df-Pk.new:
                        :filesystem(~$/<filesystem>),
                        :$total-bytes,
                        :$used-bytes,
                        :$available-bytes,
                        :capacity(~$/<capacity>),
                        :$cap-percent-num,
                        :mount-point(~$/<mount-point>),
            ;
    }
}

sub MAIN (
    Str     $servers        = 'localhost',      #= server name(s) (comma separated list)
    Int     :$above,                            #= only display file systesm above the percentage 
    Bool    :$csv,                              #= dump CSV to STDOUT
    Bool    :$html,                             #= dump HTML to STDOUT
    Bool    :$json,                             #= dump JSON to STDOUT
    Bool    :$text,                             #= TEXT print
    Bool    :$xml,                              #= dump XML to STDOUT
    Bool    :$light-mode,                       #= reverse header highlight for light-mode
            :$sort-column,                      #= sort by column # or heading
    Bool    :$sort-descending,                  #= sort in descending order
) {
    my $reverse-highlight   = $light-mode ?? True !! False;
    my $cache-file-name     = cache-file-name(:meta('drf preferences'));
    my $cache               = cache(:$cache-file-name);
    $cache                  = from-json($cache) if $cache;
    without $light-mode {
        $reverse-highlight  = $cache<light-mode> if $cache<light-mode>:exists;
    }
    cache(:$cache-file-name, :data(to-json({light-mode => $reverse-highlight})));
    my @servers             = $servers.split(',');
    my %command;
    for @servers -> $server {
        %command{$server}   = ['ssh', $server, '/usr/bin/df', '-Pk'];
    }
    my $command-manager = Async::Command::Multi.new(:%command, :20time-out, :8batch);
    $command-manager.sow;
    my %results = $command-manager.reap;
    for %results.keys.sort -> $server {
        df-Pk-grammar.parse(%results{$server}.stdout-results.trim, :actions(df-Pk-actions.new(:$server))) || warn "Failed parsing of ($server): \n\n|" ~ %results{$server}.stdout-results ~ '|';
    }
    my Our::Grid    $grid  .= new: :title('df Report'), :$reverse-highlight;
    $grid.add-heading('System'),
    $grid.add-heading('Filesystem');
    $grid.add-heading('Total');
    $grid.add-heading('Used');
    $grid.add-heading('Available');
    $grid.add-heading('% Used');
    $grid.add-heading('Mount Point');

    for %server-results.keys.sort -> $server {
        for %server-results{$server}.list -> $df-record {
            next if $above && $df-record.cap-percent-num <= $above;
            $grid.add-cell(:cell(Our::Grid::Cell.new(:text($server), :bold)), :row);
            $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.filesystem),      :justification(justify-left))));
            $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.total-bytes),     :justification(justify-right),  :bytes-to-bytes-unit)));
            $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.used-bytes),      :justification(justify-right),  :bytes-to-bytes-unit)));
            $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.available-bytes), :justification(justify-right),  :bytes-to-bytes-unit)));
            if 70 <= $df-record.cap-percent-num < 80 {
                $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.capacity),    :justification(justify-right),  :foreground<yellow>)));
            }
            elsif 80 <= $df-record.cap-percent-num < 90 {
                $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.capacity),    :justification(justify-right),  :foreground<orange>)));
            }
            elsif 90 <= $df-record.cap-percent-num < 100 {
                $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.capacity),    :justification(justify-right),  :foreground<red>)));
            }
            elsif $df-record.cap-percent-num == 100 {
                $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.capacity),    :justification(justify-right),  :foreground<red>, :blink)));
            }
            else {
                $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.capacity),    :justification(justify-right))));
            }
            $grid.add-cell(:cell(Our::Grid::Cell.new(:text($df-record.mount-point),     :justification(justify-left))));
        }
    }
    if $sort-column {
        if $sort-column ~~ /^\d+$/ {
            if 2 <= $sort-column <= 4 {
                $grid.sort-by-column($sort-column, :descending($sort-descending), :numeric);
            }
            else {
                $grid.sort-by-column($sort-column, :descending($sort-descending));
            }
        }
        else {
            if $sort-column ~~ m/^ ('Total' || 'Used' || 'Available') $/ {
                $grid.sort-by-column($sort-column, :descending($sort-descending), :numeric);
            }
            else {
                $grid.sort-by-column($sort-column, :descending($sort-descending));
            }
        }
    }
    if any($text.so, $csv.so, $html.so, $json.so, $xml.so) {
        when $text  { $grid.TEXT-print; proceed; }
        when $csv   { $grid.csv-print;  proceed; }
        when $html  { $grid.html-print; proceed; }
        when $json  { $grid.json-print; proceed; }
        when $xml   { $grid.xml-print;  proceed; }
    }
    else {
        $grid.ANSI-print;
    }
}

=finish
