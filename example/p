#!/usr/bin/env raku

use lib '/home/mdevine/github.com/raku-Our-Grid/lib';
use lib '/home/mdevine/github.com/raku-Our-Redis/lib';

use Our::Grid;

my Our::Grid $grid .= new;

=comment
  PID TTY          TIME CMD
 8654 pts/2    00:00:00 bash
 8751 pts/2    00:00:00 ps

my grammar PS-grammar {
    token TOP       { <data> || <heading> }
    token heading   {
                        ^ \s*
                        $<h0>=[\w+] \s+ 
                        $<h1>=[\w+] \s+
                        $<h2>=[\w+] \s+
                        $<h3>=[\w+]
                        $
                    }
    token data      {
                        ^ \s*
                        $<d0>=[\d+] \s+
                        $<d1>=[.+?] \s+
                        $<d2>=[\d\d':'\d\d':'\d\d] \s+
                        $<d3>=[.+?] \s*
                        $
                    }
}

my class PS-actions {
    method heading ($/) {
        $grid.add-heading:  $/<h0>.Str;
        $grid.add-heading:  $/<h1>.Str;
        $grid.add-heading:  $/<h2>.Str;
        $grid.add-heading:  $/<h3>.Str;
    }
    method data ($/) {
        $grid.add-cell:     $/<d0>.Str, :superscript;
        $grid.add-cell:     $/<d1>.Str, :foreground<yellow>, :background<green>, :italic, :faint;
        $grid.add-cell:     $/<d2>.Str, :foreground<brown>;
        my $proc-name       = $/<d3>.Str;
        if $proc-name eq 'raku' {
            $grid.add-cell: $proc-name, :foreground<red>, :background<yellow>, :bold, :blink;
        }
        else {
            $grid.add-cell: $proc-name;
        }
        $grid.current-row++;
    }
}

for qx|/usr/bin/ps -e|.lines -> $ps {
    PS-grammar.parse($ps, :actions(PS-actions.new)) or warn;
}

sub MAIN (
    Bool    :$csv,  #= dump CSV to STDOUT
    Bool    :$gui,  #= Graphical User Interface
    Bool    :$html, #= dump HTML to STDOUT
    Bool    :$json, #= dump JSON to STDOUT
    Bool    :$text, #= TEXT print
    Bool    :$tui,  #= Terminal User Interface
    Bool    :$xml,  #= dump XML to STDOUT
) {
    $grid.sort-by-columns(:sort-columns([2,0]), :descending);
    when    $csv    { $grid.CSV-print   }
    when    $gui    { $grid.GUI         }
    when    $html   { $grid.HTML-print  }
    when    $json   { $grid.JSON-print  }
    when    $text   { $grid.TEXT-print  }
    when    $tui    { $grid.TUI         }
    when    $xml    { $grid.XML-print   }
    default         { $grid.ANSI-print  }
}

=finish
